transform CelsiusToFahrenheit {
    from tempCelsius oftype decimal;
    to tempFahrenheit oftype decimal;

    tempFahrenheit: (tempCelsius * 9)/5 + 32;
}

constraint monatConstraint oftype RangeConstraint {
    lowerBound: 1;
    upperBound: 12;
}

valuetype monat oftype integer {
    constraints: [monatConstraint];
}

pipeline temperaturePipeline {

    block extractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block archiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block filePicker oftype FilePicker {
        path: "/data.csv";
    }

    block textFileInterpreter oftype TextFileInterpreter {

    }

    block csvInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block rowDeleter oftype RowDeleter {
        delete: [row 1];
    }

    block columnDeleter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I,  column L, column M, column N, column O];
    }

    block tableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "Geraet" oftype integer,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype monat,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype text
        ];
    }

    block fahrenheitTransformer oftype TableTransformer {
        inputColumn: "Temperatur";
        outputColumn: "Temperatur";
        use: CelsiusToFahrenheit;
    }

    block fahrenheitTransformer2 oftype TableTransformer {
        inputColumn: "Batterietemperatur";
        outputColumn: "Batterietemperatur";
        use: CelsiusToFahrenheit;
    }

    block temperatureLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "temperatures.sqlite";
    }

    extractor
        -> archiveInterpreter
        -> filePicker
        -> textFileInterpreter
        -> csvInterpreter
        -> rowDeleter
        -> columnDeleter
        -> tableInterpreter
        -> fahrenheitTransformer
        -> fahrenheitTransformer2
        -> temperatureLoader;
}